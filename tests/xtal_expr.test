# xtal expression tests

source testutil.tcl

namespace eval tarray::test {
    proc testconstexpr {expr} {
        set e [expr $expr]
        set t [xtal::xtal $expr]
        if {$t != $e} {
            return "<$expr> failed"
        }
        # Verify constant folding works
        if {[xtal::translate $expr] ne "return -level 0 [expr $expr]\n"} {
            return "<$expr> constant folding failed"
        }
        return
    }

    proc testexpr {xtal_expr values} {
        dict with values {}
        set expr [regsub -all {[_[:alpha:]]+} $xtal_expr \$\\0]
        if {[expr $expr] != [xtal::xtal $xtal_expr]} {
            return "<$expr> failed"
        }
        return
    }
    set testnum 0
    # TBD - floating point using exponent format
    set values {
        zero 0 one 1 two 2 three 3 four 4 minusone -1 minustwo -2
        dzero 0.0 done_one 1.1 dtwo_two 2.2 dthree_three 3.3 dfour_four 4.4 dminusone_one -1.1 dminustwo_two -2.2
        mask_ones 0xffffffff mask_a 0xaaaaaaaa
        wide_int 0x100000000 wide_max 0x7fffffffffffffff wide_min -9223372036854775808 
    }

    ### Test unary ops
    set testnum 0
    foreach {expression desc} {
        one            "Simple value"
        
        -one           "Unary minus"
        "+ two"        "Unary plus"
        "- minusone"   "Unary minus of negative"
        +minusone      "Unary plus of negative"
        --two          "Unary minus minus"
        ++two          "Unary plus plus"
        --minustwo     "Unary minus minus of negative"
        "+ + minustwo" "Unary plus plus of negative"
        -+two          "Unary minus plus"
        +-two          "Unary plus minus"
        -+minustwo     "Unary minus plus of negative"
        " +- minustwo" "Unary plus minus of negative"
        ---two         "Unary minus minus minus"
        +++two         "Unary plus plus plus"

        !zero          "Boolean negate"
        "! one"        "Boolean negate"
        "! ! zero"     "Boolean double negate with spaces"
        !!two          "Boolean double negate of integer"
        !minusone      "Boolean double negate of negative number"
        !!!zero        "Boolean triple negate"
        !dzero         "Boolean negate of double"
        !!done_one     "Boolean double negate of double"
        
        ~zero          "Complement 0"
        "~ one"        "Complement 1"
        "~ ~ zero"     "Double complement"
        ~mask_ones     "Complement all 1's"
        ~mask_a        "Complement alternating 1's"
        ~minusone      "Complement negative number"

        -!zero         "Unary combination -!"
        !-one          "Unary combination !-"
        -!~mask_ones   "Unary combination -!~"
        -+!zero         "Unary combination -+!"
        
    } {
        test xtal-unop-1.$testnum "$desc - $expression" -body [list testexpr $expression $values]
        set constexpr [string map $values $expression]
        test xtal-constunop-1.$testnum $constexpr -body [list testconstexpr $constexpr]
        incr testnum
    }

    # Unary failure tests
    # TBD - string fails
    foreach {expression desc} {
        ~dzero         "Complement double"
    } {
        test xtal-unop-1.$testnum "$desc - $expression" -body [list testexpr $expression $values] -result "can't use floating-point value as operand*" -match glob -returnCodes error
        set constexpr [string map $values $expression]
        test xtal-constunop-1.$testnum $constexpr -body [list testconstexpr $constexpr] -result "can't use floating-point value as operand*" -match glob  -returnCodes error
        incr testnum
    }
    

    #
    # Test binary operators
    set testnum 0
    foreach {expression desc} {
        one+one       "Binary +"
        two+minusone  "Binary + unary"
        two+-2        "Binary + unary constant"
        "minusone + - 2"  "Binary + unary constant"
        "+2- three"      "Binary + unary constant"

        done_one+done_one       "Binary +"
        dtwo_two+dminusone_one  "Binary + unary"
        dtwo_two+-2        "Binary + unary constant"
        "dminusone_one + - 2"  "Binary + unary constant"
        "+2- dthree_three"      "Binary + unary constant"
        
        wide_int+wide_int          "Binary +"
        "wide_int + wide_min"  "Binary + unary"
        wide_max+-2        "Binary + unary constant"
        "wide_min + + +2"  "Binary + unary constant"
        "-2 + wide_max"    "Binary + unary constant"
    } {
        test xtal-binop-1.$testnum "$desc - $expression" -body [list testexpr $expression $values]
        set constexpr [string map $values $expression]
        test xtal-constbinop-1.$testnum $constexpr -body [list testconstexpr $constexpr]
        incr testnum
    }

    #
    # Test operator associativity
    foreach {expression desc} {
        {four-two+two} "+- Left associativity"
        {four-two-two} "- Left associativity"
        {one+two*four} "+* Operator precedence"
        {one||zero&&zero} "Logical operator precedence"
    } {
        test xtal-assoc-1.$testnum "$desc - $expression" -body [list testexpr $expression $values]
        set constexpr [string map $values $expression]
        test xtal-constassoc-1.$testnum $constexpr -body [list testconstexpr $constexpr]
        incr testnum
    }
}

::tcltest::cleanupTests
