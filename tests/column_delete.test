# TBD - shared obj tests should check original object not modified
# TBD - tests that traces are triggered

source testutil.tcl

namespace eval tarray::test {
    namespace import ::tcltest::test

    # Note two general types of tests - via a temp variable and directly
    # from the create return value. The difference is that in the
    # former case the value ref count is > 1, and the object needs to
    # be dup'ed while in the latter the ref count is 1 and the called
    # command can directly use it.
    # Also, when modifying a temp variable tests have to confirm that
    # the original is not modified

    # Convenience proc to verify that result is as expected and original
    # is not changed
    proc delete_verify {type orig_col orig_list new_col new_list} {
        if {! [clequal $orig_col $type $orig_list]} { return 1 }
        if {! [clequal $new_col $type $new_list]} { return 2 }
        return 0
    }

    #
    # column delete index

    foreach type {boolean any byte double int uint wide} {

        test column_delete-index-$type-1.0 {
            Test deletion of first element (shared tclobj)
        } -body {
            set ta [tarray::column create $type $known]
            delete_verify $type $ta $known [tarray::column delete $ta 0] [lrange $known 1 end]
        } -result 0

        test column_delete-index-$type-1.1 {
            Test deletion of first element (unshared tclobj)
        } -body {
            tarray::column delete [tarray::column create $type $known] 0
        } -result [crep $type [lrange $known 1 end]] -match column

        test column_delete-index-$type-2.0 {
            Test deletion of last element (shared tclobj)
        } -body {
            set ta [tarray::column create $type $known]
            delete_verify $type $ta $known [tarray::column delete $ta [expr {[llength $known]-1}]] [lrange $known 0 end-1]
        } -result 0

        test column_delete-index-$type-2.1 {
            Test deletion of last element (unshared tclobj)
        } -body {
            tarray::column delete [tarray::column create $type $known] [expr {[llength $known]-1}]
        } -result [crep $type [lrange $known 0 end-1]] -match column

        test column_delete-index-$type-3.0 {
            Test deletion of end element (shared tclobj)
        } -body {
            set ta [tarray::column create $type $known]
            delete_verify $type $ta $known [tarray::column delete $ta end] [lrange $known 0 end-1]
        } -result 0

        test column_delete-index-$type-3.1 {
            Test deletion of end element (unshared tclobj)
        } -body {
            tarray::column delete [tarray::column create $type $known] end
        } -result [crep $type [lrange $known 0 end-1]] -match column

        set n -1
        foreach i {1 7 8 9 31 32 33 63 64 65 254} {
            test column_delete-index-$type-4.[incr n] {
                Test deletion of intermediate element (shared tclobj)
            } -body {
                set ta [tarray::column create $type $known]
                delete_verify $type $ta $known [tarray::column delete $ta $i] [concat [lrange $known 0 $i-1] [lrange $known $i+1 end]]
            } -result 0

            test column_delete-index-$type-4.[incr n] {
                Test deletion of intermediate element (unshared tclobj)
            } -body {
                tarray::column delete [tarray::column create $type $known] $i
            } -result [crep $type [concat [lrange $known 0 $i-1] [lrange $known $i+1 end]]] -match column
        }

        test column_delete-index-$type-5.0 {
            Test deletion of out-of-range index
        } -body {
            tarray::column delete [tarray::column create $type $known] [expr {[llength $known] + 10}]
        } -result [crep $type $known] -match column

        test column_delete-index-$type-5.1 {
            Test deletion of out-of-range index (+1)
        } -body {
            tarray::column delete [tarray::column create $type $known] [llength $known]
        } -result [crep $type $known] -match column

        test column_delete-index-$type-5.2 {
            Test deletion of negative index
        } -body {
            tarray::column delete [tarray::column create $type $known] -1
        } -result [crep $type $known] -match column

        test column_delete-index-$type-5.3 {
            Test deletion of invalid syntax index
        } -body {
            tarray::column delete [tarray::column create $type $known] nonintsyntax
        } -returnCodes error -match glob -result *

        test column_delete-index-$type-6.0 {
            Test deletion of element from single element column (shared)
        } -body {
            set l [lrange $known 0 0]
            set ta [tarray::column create $type $l]
            delete_verify $type $ta $l [tarray::column delete $ta 0] {}
        } -result 0

        test column_delete-index-$type-6.1 {
            Test deletion of element from single element column (unshared)
        } -body {
            tarray::column delete [tarray::column create $type [lrange $known 0 0]] end
        } -result [crep $type {}] -match column

        test column_delete-index-$type-7.0 {
            Test attempt to delete element from empty column (shared)
        } -body {
            set ta [tarray::column create $type {}]
            delete_verify $type $ta {} [tarray::column delete $ta 0] {}
        } -result 0

        test column_delete-index-$type-7.1 {
            Test deletion of invalid index from empty column (unshared)
        } -body {
            tarray::column delete [tarray::column create $type {}] -1
        } -result [crep $type {}] -match column
    }

    #
    # column delete index list

    foreach type {boolean any byte double int uint wide} {

        test column_delete-indexlist-$type-1.0 {
            Test deletion of leading elements (shared tclobj)
        } -body {
            set ta [tarray::column create $type $known]
            delete_verify $type $ta $known [tarray::column delete $ta {0 1}] [lrange $known 2 end]
        } -result 0 

        test column_delete-indexlist-$type-1.1 {
            Test deletion of leading elements (unshared tclobj)
        } -body {
            tarray::column delete [tarray::column create $type $known] {0 1 2}
        } -result [crep $type [lrange $known 3 end]] -match column

        test column_delete-indexlist-$type-1.2 {
            Test deletion of leading elements (shared tclobj, out of order)
        } -body {
            set ta [tarray::column create $type $known]
            delete_verify $type $ta $known [tarray::column delete $ta {0 2 1 3}] [lrange $known 4 end]
        } -result 0

        test column_delete-indexlist-$type-1.3 {
            Test deletion of leading elements (unshared tclobj, reverse order)
        } -body {
            tarray::column delete [tarray::column create $type $known] {2 1 0}
        } -result [crep $type [lrange $known 3 end]] -match column

        test column_delete-indexlist-$type-2.0 {
            Test deletion of trailing elements (shared tclobj)
        } -body {
            set i [llength $known]
            incr i -2
            set ta [tarray::column create $type $known]
            delete_verify $type $ta $known [tarray::column delete $ta [list $i [incr i]]] [lrange $known 0 end-2]
        } -result 0

        test column_delete-indexlist-$type-2.1 {
            Test deletion of trailing elements (unshared tclobj)
        } -body {
            set i [llength $known]
            incr i -3
            tarray::column delete [tarray::column create $type $known] [list $i [incr i] [incr i]] 
        } -result [crep $type [lrange $known 0 end-3]] -match column

        test column_delete-indexlist-$type-2.2 {
            Test deletion of trailing elements (shared tclobj, out of order)
        } -body {
            set i [llength $known]
            set ta [tarray::column create $type $known]
            delete_verify $type $ta $known [tarray::column delete $ta [list [incr i -1] [incr i -2] [incr i]]] [lrange $known 0 end-3]
        } -result 0

        test column_delete-indexlist-$type-2.3 {
            Test deletion of trailing elements (unshared tclobj, reverse order)
        } -body {
            set i [llength $known]
            tarray::column delete [tarray::column create $type $known] [list [incr i -1] [incr i -1] [incr i -1]]
        } -result [crep $type [lrange $known 0 end-3]] -match column

        test column_delete-indexlist-$type-3.0 {
            Test deletion of intermediate (shared tclobj, ordered)
        } -body {
            set indices {1 7 8 9 31 32 33 63 64 65 254}
            set l2 $known
            # Deletion must take place back to front
            foreach i [lsort -integer -decreasing $indices] {
                set l2 [lreplace $l2 $i $i]
            }
            set ta [tarray::column create $type $known]
            delete_verify $type $ta $known [tarray::column delete $ta [lsort -increasing -integer $indices]] $l2
        } -result 0

        test column_delete-indexlist-$type-3.1 {
            Test deletion of intermediate (unshared tclobj, reverse order)
        } -body {
            set indices {1 7 8 9 31 32 33 63 64 65 254}
            set l2 $known
            # Deletion must take place back to front
            foreach i [lsort -integer -decreasing $indices] {
                set l2 [lreplace $l2 $i $i]
            }
            cequal [tarray::column delete [tarray::column create $type $known] [lsort -decreasing -integer $indices]] [crep $type $l2]
        } -result 1

        test column_delete-indexlist-$type-3.2 {
            Test deletion of intermediate (shared tclobj, random order)
        } -body {
            # Note duplicate
            set indices {1 254 64 33 7 65 8 33 9 31 32 63}
            set l2 $known
            # Deletion must take place back to front. -unique to remove dups
            foreach i [lsort -integer -decreasing -unique $indices] {
                set l2 [lreplace $l2 $i $i]
            }
            set ta [tarray::column create $type $known]
            delete_verify $type $ta $known [tarray::column delete $ta $indices] $l2
        } -result 0


        test column_delete-indexlist-$type-4.0 {
            Test deletion of out-of-range (unshared, increasing indices)
        } -body {
            set indices {1 7 8 9 31 32 33 63 64 65 254}
            set l2 $known
            # Deletion must take place back to front
            foreach i [lsort -integer -decreasing $indices] {
                set l2 [lreplace $l2 $i $i]
            }
            set i [llength $known]
            set indices [linsert [linsert $indices 0 $i] 32 [incr i 10]]
            lappend indices -1
            cequal [tarray::column delete [tarray::column create $type $known] [lsort -increasing -integer $indices]] [crep $type $l2]
        } -result 1

        test column_delete-indexlist-$type-4.1 {
            Test deletion of out-of-range (unshared, reverse indices)
        } -body {
            set indices {1 7 8 9 31 32 33 63 64 65 254}
            set l2 $known
            # Deletion must take place back to front
            foreach i [lsort -integer -decreasing $indices] {
                set l2 [lreplace $l2 $i $i]
            }
            set i [llength $known]
            set indices [linsert [linsert $indices 0 $i] 32 [incr i 10]]
            lappend indices -1
            cequal [tarray::column delete [tarray::column create $type $known] [lsort -decreasing -integer $indices]] [crep $type $l2]
        } -result 1

        test column_delete-indexlist-$type-4.2 {
            Test deletion of out-of-range (shared, random indices)
        } -body {
            # Note duplicate 33
            set indices {1 254 64 33 7 65 8 33 9 31 32 63}
            set l2 $known
            # Deletion must take place back to front
            foreach i [lsort -integer -decreasing -unique $indices] {
                set l2 [lreplace $l2 $i $i]
            }
            set i [llength $known]
            set indices [linsert [linsert $indices 0 $i] 32 [incr i 10]]
            lappend indices -1
            cequal [tarray::column delete [tarray::column create $type $known] $indices] [crep $type $l2]
        } -result 1

        test column_delete-indexlist-$type-5.0 {
            Test deletion of invalid syntax indices
        } -body {
            set indices {1 notinteger 63}
            tarray::column delete [tarray::column create $type $known] $indices
        } -returnCodes error -match glob -result *

        test column_delete-indexlist-$type-5.1 {
            Test deletion of end keyword
        } -body {
            # Note "end" is invalid in a index list
            set indices {1 end 63}
            tarray::column delete [tarray::column create $type $known] $indices
        } -returnCodes error -match glob -result *

        test column_delete-indexlist-$type-6.0 {
            Test deletion of all indices from column (shared)
        } -body {
            set i -1
            set indices {}
            time {lappend indices [incr i]} 256
            set l [lrange $known 0 3]
            set ta [tarray::column create $type $l]
            delete_verify $type $ta $l [tarray::column delete $ta $indices] {}
        } -result 0

        test column_delete-indexlist-$type-6.1 {
            Test deletion of all indices from column (shared)
        } -body {
            set i -1
            set indices {}
            time {lappend indices [incr i]} 256
            set l [lrange $known 0 3]
            set ta [tarray::column create $type $l]
            delete_verify $type $ta $l [tarray::column delete $ta [lsort -integer -decreasing $indices]] {}
        } -result 0

        test column_delete-indexlist-$type-6.2 {
            Test deletion of all indices from column (shared)
        } -body {
            set l [lrange $known 0 3]
            set ta [tarray::column create $type $l]
            delete_verify $type $ta $l [tarray::column delete $ta {0 1 2 3}] {}
        } -result 0

        test column_delete-indexlist-$type-6.2 {
            Test deletion of all indices from column (unshared, random order)
        } -body {
            set l [lrange $known 0 3]
            set ta [tarray::column create $type $l]
            delete_verify $type $ta $l [tarray::column delete $ta {1 0 3 2}] {}
        } -result 0

        test column_delete-indexlist-$type-7.0 {
            Test attempt to delete element from empty column (shared)
        } -body {
            set ta [tarray::column create $type {}]
            delete_verify $type $ta {} [tarray::column delete $ta {0 1 2}] {}
        } -result 0

        test column_delete-indexlist-$type-7.1 {
            Test attempt to delete element from empty column (unshared)
        } -body {
            tarray::column delete [tarray::column create $type {}] {0 2 1}
        } -result [crep $type {}] -match column

        test column_delete-indexlist-$type-8.0 {
            Delete duplicate indices (shared)
        } -body {
            set ta [tarray::column create $type $known]
            delete_verify $type $ta $known [tarray::column delete $ta {0 0 1}] [lrange $known 2 end]
        } -result 0

        test column_delete-indexlist-$type-8.1 {
            Delete duplicate indices (unshared)
        } -body {
            tarray::column delete [tarray::column create $type $known] {4 4}
        } -result [crep $type [concat [lrange $known 0 3] [lrange $known 5 end]]] -match column
    }

    #
    # column delete indicesarray

    foreach type {boolean any byte double int uint wide} {

        test column_delete-indices-$type-1.0 {
            Test deletion of leading elements (shared tclobj)
        } -body {
            set ta [tarray::column create $type $known]
            delete_verify $type $ta $known [tarray::column delete $ta [tarray::column create int {0 1}]] [lrange $known 2 end]
        } -result 0

        test column_delete-indices-$type-1.1 {
            Test deletion of leading elements (unshared tclobj)
        } -body {
            tarray::column delete [tarray::column create $type $known] [tarray::column create int {0 1 2}]
        } -result [crep $type [lrange $known 3 end]] -match column

        test column_delete-indices-$type-1.2 {
            Test deletion of leading elements (shared tclobj, out of order)
        } -body {
            set ta [tarray::column create $type $known]
            delete_verify $type $ta $known [tarray::column delete $ta [tarray::column create int {0 2 1 3}]] [lrange $known 4 end]
        } -result 0

        test column_delete-indices-$type-1.3 {
            Test deletion of leading elements (unshared tclobj, reverse order)
        } -body {
            tarray::column delete [tarray::column create $type $known] [tarray::column create int {2 1 0}]
        } -result [crep $type [lrange $known 3 end]] -match column

        test column_delete-indices-$type-2.0 {
            Test deletion of trailing elements (shared tclobj)
        } -body {
            set i [llength $known]
            incr i -2
            set ta [tarray::column create $type $known]
            delete_verify $type $ta $known [tarray::column delete $ta [tarray::column create int [list $i [incr i]]]] [lrange $known 0 end-2]
        } -result 0

        test column_delete-indices-$type-2.1 {
            Test deletion of trailing elements (unshared tclobj)
        } -body {
            set i [llength $known]
            incr i -3
            tarray::column delete [tarray::column create $type $known] [tarray::column create int [list $i [incr i] [incr i]] ]
        } -result [crep $type [lrange $known 0 end-3]] -match column

        test column_delete-indices-$type-2.2 {
            Test deletion of trailing elements (shared tclobj, out of order)
        } -body {
            set i [llength $known]
            set ta [tarray::column create $type $known]
            delete_verify $type $ta $known [tarray::column delete $ta [list [incr i -1] [incr i -2] [incr i]]] [lrange $known 0 end-3]
        } -result 0

        test column_delete-indices-$type-2.3 {
            Test deletion of trailing elements (unshared tclobj, reverse order)
        } -body {
            set i [llength $known]
            tarray::column delete [tarray::column create $type $known] [tarray::column create int [list [incr i -1] [incr i -1] [incr i -1]]]
        } -result [crep $type [lrange $known 0 end-3]] -match column

        test column_delete-indices-$type-3.0 {
            Test deletion of intermediate (shared tclobj, ordered)
        } -body {
            set indices {1 7 8 9 31 32 33 63 64 65 254}
            set l2 $known
            # Deletion must take place back to front
            foreach i [lsort -integer -decreasing $indices] {
                set l2 [lreplace $l2 $i $i]
            }
            set ta [tarray::column create $type $known]
            # Explicitly order indices
            set indices [tarray::column sort [tarray::column create int $indices]]
            delete_verify $type $ta $known [tarray::column delete $ta $indices] $l2
        } -result 0

        test column_delete-indices-$type-3.1 {
            Test deletion of intermediate (unshared tclobj, reverse order)
        } -body {
            set indices {1 7 8 9 31 32 33 63 64 65 254}
            set l2 $known
            # Deletion must take place back to front
            foreach i [lsort -integer -decreasing $indices] {
                set l2 [lreplace $l2 $i $i]
            }
            set indices [tarray::column sort -decreasing [tarray::column create int $indices]]
            cequal [tarray::column delete [tarray::column create $type $known] $indices] [crep $type $l2]
        } -result 1

        test column_delete-indices-$type-3.2 {
            Test deletion of intermediate (shared tclobj, random order)
        } -body {
            # Note duplicate
            set indices {1 254 64 33 7 65 8 33 9 31 32 63}
            set l2 $known
            # Deletion must take place back to front. -unique to remove dups
            foreach i [lsort -integer -decreasing -unique $indices] {
                set l2 [lreplace $l2 $i $i]
            }
            set ta [tarray::column create $type $known]
            delete_verify $type $ta $known [tarray::column delete $ta [tarray::column create int $indices]] $l2
        } -result 0

        test column_delete-indices-$type-4.0 {
            Test deletion of out-of-range (unshared, increasing indices)
        } -body {
            set indices {1 7 8 9 31 32 33 63 64 65 254}
            set l2 $known
            # Deletion must take place back to front
            foreach i [lsort -integer -decreasing $indices] {
                set l2 [lreplace $l2 $i $i]
            }
            set i [llength $known]
            set indices [linsert [linsert $indices 0 $i] 32 [incr i 10]]
            lappend indices -1
            set indices [tarray::column sort -increasing [tarray::column create int $indices]]
            cequal [tarray::column delete [tarray::column create $type $known] $indices] [crep $type $l2]
        } -result 1

        test column_delete-indices-$type-4.1 {
            Test deletion of out-of-range (unshared, reverse indices)
        } -body {
            set indices {1 7 8 9 31 32 33 63 64 65 254}
            set l2 $known
            # Deletion must take place back to front
            foreach i [lsort -integer -decreasing $indices] {
                set l2 [lreplace $l2 $i $i]
            }
            set i [llength $known]
            set indices [linsert [linsert $indices 0 $i] 32 [incr i 10]]
            lappend indices -1
            set indices [tarray::column sort -decreasing [tarray::column create int $indices]]
            cequal [tarray::column delete [tarray::column create $type $known] $indices] [crep $type $l2]
        } -result 1

        test column_delete-indices-$type-4.2 {
            Test deletion of out-of-range (shared, random indices)
        } -body {
            # Note duplicate 33
            set indices {1 254 64 33 7 65 8 33 9 31 32 63}
            set l2 $known
            # Deletion must take place back to front
            foreach i [lsort -integer -decreasing -unique $indices] {
                set l2 [lreplace $l2 $i $i]
            }
            set i [llength $known]
            set indices [linsert [linsert $indices 0 $i] 32 [incr i 10]]
            lappend indices -1
            cequal [tarray::column delete [tarray::column create $type $known] $indices] [crep $type $l2]
        } -result 1

        test column_delete-indices-$type-5.0 {
            Test deletion of invalid syntax indices
        } -body {
            set indices {1 notinteger 63}
            tarray::column delete [tarray::column create $type $known] $indices
        } -returnCodes error -match glob -result *

        test column_delete-indices-$type-5.1 {
            Test deletion of end keyword
        } -body {
            # Note "end" is invalid in a index list
            set indices {1 end 63}
            tarray::column delete [tarray::column create $type $known] $indices
        } -returnCodes error -match glob -result *

        test column_delete-indices-$type-6.0 {
            Test deletion of all indices from column (shared)
        } -body {
            set i -1
            set indices {}
            time {lappend indices [incr i]} 256
            set l [lrange $known 0 3]
            set ta [tarray::column create $type $l]
            delete_verify $type $ta $l [tarray::column delete $ta $indices] {}
        } -result 0

        test column_delete-indices-$type-6.1 {
            Test deletion of all indices from column (shared)
        } -body {
            set i -1
            set indices {}
            time {lappend indices [incr i]} 256
            set l [lrange $known 0 3]
            set ta [tarray::column create $type $l]
            delete_verify $type $ta $l [tarray::column delete $ta [lsort -integer -decreasing $indices]] {}
        } -result 0

        test column_delete-indices-$type-6.2 {
            Test deletion of all indices from column (shared)
        } -body {
            set l [lrange $known 0 3]
            set ta [tarray::column create $type $l ]
            delete_verify $type $ta $l [tarray::column delete $ta {0 1 2 3}] {}
        } -result 0

        test column_delete-indices-$type-6.2 {
            Test deletion of all indices from column (unshared, random order)
        } -body {
            tarray::column delete [tarray::column create $type [lrange $known 0 3]] {1 0 3 2}
        } -result [crep $type {}] -match column

        test column_delete-indices-$type-7.0 {
            Test attempt to delete element from empty column (shared)
        } -body {
            set ta [tarray::column create $type {}]
            delete_verify $type $ta {} [tarray::column delete $ta {0 1 2}] {}
        } -result 0

        test column_delete-indices-$type-7.1 {
            Test attempt to delete element from empty column (unshared)
        } -body {
            tarray::column delete [tarray::column create $type {}] {0 2 1}
        } -result [crep $type {}] -match column

        test column_delete-indices-$type-8.0 {
            Delete duplicate indices (shared)
        } -body {
            set ta [tarray::column create $type $known]
            delete_verify $type $ta $known [tarray::column delete $ta {0 0 1}] [lrange $known 2 end]
        } -result 0

        test column_delete-indices-$type-8.1 {
            Delete duplicate indices (unshared)
        } -body {
            tarray::column delete [tarray::column create $type $known] {4 4}
        } -result [crep $type [concat [lrange $known 0 3] [lrange $known 5 end]]] -match column
    }


    #
    # column delete range

    foreach type {boolean any byte double int uint wide} {

        test column_delete-range-$type-1.0 {
            Test deletion of leading elements (shared tclobj)
        } -body {
            set ta [tarray::column create $type $known]
            delete_verify $type $ta $known [tarray::column delete $ta 0 1] [lrange $known 2 end]
        } -result 0

        test column_delete-range-$type-1.1 {
            Test deletion of leading elements (unshared tclobj)
        } -body {
            tarray::column delete [tarray::column create $type $known] 0 63
        } -result [crep $type [lrange $known 64 end]] -match column

        test column_delete-range-$type-2.0 {
            Test deletion of trailing elements (shared tclobj)
        } -body {
            set i [llength $known]
            incr i -1
            set ta [tarray::column create $type $known]
            delete_verify $type $ta $known [tarray::column delete $ta 127 $i] [lrange $known 0 126]
        } -result 0

        test column_delete-range-$type-2.1 {
            Test deletion of trailing elements (unshared tclobj, using end)
        } -body {
            tarray::column delete [tarray::column create $type $known] 129 end
        } -result [crep $type [lrange $known 0 128]] -match column

        test column_delete-range-$type-3.0 {
            Test deletion of intermediate (shared tclobj)
        } -body {
            set ta [tarray::column create $type $known]
            delete_verify $type $ta $known [tarray::column delete $ta 63 127] [concat [lrange $known 0 62] [lrange $known 128 end]]
        } -result 0

        test column_delete-range-$type-3.1 {
            Test deletion of intermediate (unshared tclobj)
        } -body {
            tarray::column delete [tarray::column create $type $known] 64 128
        } -result [crep $type [concat [lrange $known 0 63] [lrange $known 129 end]]] -match column

        test column_delete-range-$type-4.0 {
            Test deletion of out-of-range (unshared)
        } -body {
            set i [llength $known]
            tarray::column delete [tarray::column create $type $known] $i [incr i 10]
        } -result [crep $type $known] -match column

        test column_delete-range-$type-4.1 {
            Test deletion of out-of-range (unshared, negative)
        } -body {
            tarray::column delete [tarray::column create $type $known] -4 2
        } -result [crep $type [lrange $known 3 end]] -match column

        test column_delete-range-$type-4.2 {
            Test deletion of out-of-range (unshared, partial)
        } -body {
            set i [llength $known]
            tarray::column delete [tarray::column create $type $known] 130 [incr i 100]
        } -result [crep $type [lrange $known 0 129]] -match column

        test column_delete-range-$type-4.3 {
            Test deletion of out-of-range (unshared, partial negative)
        } -body {
            tarray::column delete [tarray::column create $type $known] -10 0
        } -result [crep $type [lrange $known 1 end]] -match column

        test column_delete-range-$type-4.4 {
            Test deletion of reversed range limits
        } -body {
            tarray::column delete [tarray::column create $type $known] 60 40
        } -result [crep $type $known] -match column

        test column_delete-range-$type-5.0 {
            Test deletion of invalid syntax start index
        } -body {
            tarray::column delete [tarray::column create $type $known] foo 10
        } -returnCodes error -match glob -result *

        test column_delete-range-$type-5.1 {
            Test deletion of invalid syntax end index
        } -body {
            tarray::column delete [tarray::column create $type $known] 0 foo
        } -returnCodes error -match glob -result *

        test column_delete-range-$type-6.0 {
            Test deletion of all indices from column (shared)
        } -body {
            set ta [tarray::column create $type $known]
            delete_verify $type $ta $known [tarray::column delete $ta 0 end] {}
        } -result 0

        test column_delete-range-$type-6.2 {
            Test deletion of all indices from column (unshared, random order)
        } -body {
            set i [llength $known]
            tarray::column delete [tarray::column create $type [lrange $known 0 3]] 0 [incr i -1]
        } -result [crep $type {}] -match column

        test column_delete-range-$type-7.0 {
            Test attempt to delete element from empty column (shared)
        } -body {
            set ta [tarray::column create $type {}]
            delete_verify $type $ta {} [tarray::column delete $ta 0 0] {}
        } -result 0

        test column_delete-range-$type-7.1 {
            Test attempt to delete element from empty column (unshared)
        } -body {
            tarray::column delete [tarray::column create $type {}] 0 end
        } -result [crep $type {}] -match column

        test column_delete-range-$type-8.0 {
            Delete single element range (shared, leading)
        } -body {
            set ta [tarray::column create $type $known]
            delete_verify $type $ta $known [tarray::column delete $ta 0 0] [lrange $known 1 end]
        } -result 0

        test column_delete-range-$type-8.1 {
            Delete single element range (unshared, trailing)
        } -body {
            tarray::column delete [tarray::column create $type $known] end end
        } -result [crep $type [lrange $known 0 end-1]] -match column

        test column_delete-range-$type-8.1 {
            Delete single element range (unshared, random)
        } -body {
            tarray::column delete [tarray::column create $type $known] 32 32
        } -result [crep $type [concat [lrange $known 0 31] [lrange $known 33 end]]] -match column
    }

    ::tcltest::cleanupTests
}
