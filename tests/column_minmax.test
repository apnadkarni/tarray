# column search tests

source testutil.tcl

namespace eval tarray::test {
    foreach type {any string boolean byte double int uint wide} {
        test column_minmax-$type-1.0 {
            Minmax an empty column
        } -body {
            tarray::column minmax [newcolumn $type]
        } -result "Invalid index range limit ''." -returnCodes error

        test column_minmax-$type-1.1 {
            Minmax an empty range
        } -body {
            tarray::column minmax -range {5 4} [samplecolumn $type]
        } -result "Invalid index range limit ''." -returnCodes error

        test column_minmax-$type-1.2 {
            Minmax an empty range
        } -body {
            set col [samplecolumn $type]
            set n [tarray::column size $col]
            tarray::column minmax -range [list $n $n] $col
        } -result "Invalid index range limit ''." -returnCodes error

        test column_minmax-$type-2.0 {
            Minmax
        } -body {
            set l [largelist $type]
            set sorted [lsort [lsort_cmp $type] $l]
            set min [lindex $sorted 0]
            set max [lindex $sorted end]
            lassign [tarray::column minmax [newcolumn $type $l]] tmin tmax
            list [expr {$tmin == $min}] [expr {$tmax == $max}]
        } -result {1 1}

        test column_minmax-$type-3.0 {
            Minmax -indices
        } -body {
            set l [largelist $type]
            set sorted [lsort [lsort_cmp $type] $l]
            lassign [tarray::column minmax [newcolumn $type $l]] tmin tmax
            lassign [tarray::column minmax -indices [newcolumn $type $l]] imin imax
            list [expr {$tmin == [lindex $l $imin]}] [expr {$tmax == [lindex $l $imax]}]
        } -result {1 1}

        test column_minmax-$type-3.1 {
            Minmax - verify first element returned
        } -body {
            set col [newcolumn $type [lrepeat 100000 [samplevalue $type]]]
            tarray::column minmax -indices $col
        } -result {0 0}

        test column_minmax-$type-3.0 {
            Minmax -range
        } -body {
            set l [largelist $type]
            set sorted [lsort [lsort_cmp $type] $l]
            lassign [tarray::column minmax -range {100 90000} [newcolumn $type $sorted]] tmin tmax
            list [expr {$tmin == [lindex $sorted 100]}] [expr {$tmax == [lindex $sorted 90000]}]
        } -result {1 1}
    }

    foreach type {any string} {
        test column_minmax-$type-4.0 {
            minmax -nocase unspecified
        } -body {
            set l {a A B b}
            tarray::column minmax [newcolumn $type $l]
        } -result {A b}

        test column_minmax-$type-4.1 {
            minmax -nocase
        } -body {
            set l {a A B b}
            tarray::column minmax -nocase [newcolumn $type $l]
        } -result {a B}

    }

}

::tcltest::cleanupTests
