# column sum tests

source testutil.tcl

namespace eval tarray::test {
    foreach type {any string boolean} {
        test column_sum-$type-1.0 {
            Sum column of wrong type
        } -body {
            tarray::column sum [newcolumn $type]
        } -result "tarray is of the wrong type ($type)" -returnCodes error
    }

    foreach type {byte double int uint wide} {
        test column_sum-$type-1.0 {
            Sum an empty column
        } -body {
            tarray::column sum [newcolumn $type]
        } -result [expr {$type eq "double" ? 0.0 : 0}]

        test column_sum-$type-2.0 {
            Sum a column
        } -body {
            tarray::column sum [samplecolumn $type]
        } -result [tcl::mathop::+ {*}[samplerange $type]]
    }

    foreach type {byte int uint} {
        test column_sum-$type-2.1 {
            Sum a column
        } -body {
            tarray::column sum [largecolumn $type]
        } -result [tcl::mathop::+ {*}[largelist $type]]
    }

    # wides tested separately as in Tcl they get converted to bignum
    test column_sum-wide-2.1 {
        Sum a column
    } -body {
        tarray::column sum [largecolumn wide]
    } -result [expr {wide([tcl::mathop::+ {*}[largelist wide]])}]
    
    test column_sum-int-3.0 {
            Sum a column and make sure int handles correctly for overflow
    } -body {
        tarray::column sum [tarray::column create uint {0x7fffffff 1}]
    } -result 2147483648

    test column_sum-uint-3.0 {
        Sum a column and make sure unsigned handles correctly for overflow
    } -body {
        tarray::column sum [tarray::column create uint {0xffffffff 1}]
    } -result 4294967296
}
